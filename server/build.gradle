plugins {
    id "org.flywaydb.flyway" version "5.2.4"
}

flywayMigrate.dependsOn classes

description = "CloudFoundry Identity Server JAR"

configurations {
    flywayMigration
}

dependencies {

    compile project(":cloudfoundry-identity-model")

    compile libraries.tomcatJdbc
    provided libraries.tomcatEmbed
    compile libraries.javaxMail

    compile libraries.jsonPath
    compile libraries.zxing
    compile libraries.springBeans
    compile libraries.springContext
    compile libraries.springContextSupport
    compile libraries.springTx
    compile libraries.springJdbc
    compile libraries.springWeb
    compile libraries.springSecurityCore
    compile libraries.springSecurityJwt
    compile libraries.springSecurityWeb
    compile libraries.springSecuritySaml
    compile libraries.springSessionJdbc

    compile(libraries.springSecurityOauth) {
        exclude(module: "commons-codec")
        exclude(module: "jackson-mapper-asl")
        exclude(module: "spring-security-web")
    }

    compile libraries.bouncyCastleProv
    compile libraries.bouncyCastlePkix

    compile libraries.guava

    compile libraries.aspectJRt
    compile libraries.aspectJWeaver

    compile libraries.thymeleafSpring5
    compile libraries.thymeleafDialect
    compile libraries.thymeleafExtrasSpringSecurity5

    compile(libraries.unboundIdScimSdk) {
        exclude(module: "servlet-api")
        exclude(module: "commons-logging")
        exclude(module: "httpclient")
        exclude(module: "wink-client-apache-httpclient")
    }

    compile libraries.hibernateValidator
    compile libraries.mariaJdbcDriver
    compile libraries.flywayCore
    compile libraries.hsqldb

    compile libraries.snakeyaml

    compile libraries.springSecurityLdap
    compile libraries.springLdapCore
    compile libraries.springLdapCoreTiger
    compile(libraries.apacheLdapApi) {
        exclude(module: "slf4j-api")
    }

    compile libraries.passay

    compile libraries.googleAuth

    compile libraries.slf4jImpl
    compile libraries.log4jCore

    implementation libraries.javaxXmlBindApi
    implementation libraries.javaxXmlBindCore
    implementation libraries.javaxXmlBindImpl

    testCompile project(":cloudfoundry-identity-model").sourceSets.test.output

    testCompile libraries.springTest

    testCompile(libraries.junit)
    testCompile(libraries.mockito)

    testCompile libraries.mockitoJunit5

    testCompile libraries.postgresql

    testCompile libraries.tomcatElApi
    testCompile libraries.tomcatJasperEl
    testCompile libraries.tomcatJdbc

    testCompile libraries.jsonPathAssert

    flywayMigration libraries.springContext
}

configurations.all {
    exclude group: "org.beanshell", module: "bsh-core"
}

jar {
    exclude "org/cloudfoundry/identity/uaa/web/tomcat/UaaStartupFailureListener.*"
}

processResources {
    //maven replaces project.artifactId in the log4j.properties file
    //https://www.pivotaltracker.com/story/show/74344574
    filter { line -> line.contains('${project.artifactId}') ? line.replace('${project.artifactId}', 'cloudfoundry-identity-server') : line }
}

integrationTest {}.onlyIf { //disable since we don't have any
    false
}

task tomcatListenerJar(type: Jar) {
    archiveBaseName = "tomcat-listener"
    from(sourceSets.main.output)
    include "org/cloudfoundry/identity/uaa/web/tomcat/UaaStartupFailureListener.*"
}

artifacts {
    archives tomcatListenerJar
}

task migrationJar(type: Jar) {
    manifest.attributes("Main-Class": "org.cloudfoundry.identity.uaa.db.CrazyMain")
    manifest.attributes("Class-Path": "spring-context-5.2.6.RELEASE.jar spring-beans-5.2.6.RELEASE.jar")
    archiveBaseName = "flyway-migrations"
    from(sourceSets.main.output)
    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }
    include "org/cloudfoundry/identity/uaa/db/**/*"
    include "spring/env.xml"
    include "spring/data-source.xml"
    include "spring/jdbc-test-base-add-flyway.xml"
}

artifacts {
    archives migrationJar
}
